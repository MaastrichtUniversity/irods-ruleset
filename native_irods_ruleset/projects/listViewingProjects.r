# Call with
#
# irule -F listViewingProjects.r

listViewingProjects {
    *json_str = '[]';
    *size = 0;

    # I did not manage to get this query to work in a LIGQ way directly in foreach(). It fails over
    # the `in` condition with parentheses.
    # SELECT COLL_NAME WHERE COLL_ACCESS_NAME in ('read object', 'modify object') and COLL_PARENT_NAME = '/nlmumc/projects'"

    msiMakeGenQuery("COLL_NAME", "COLL_ACCESS_NAME in ('own', 'read object', 'modify object')  and COLL_PARENT_NAME = '/nlmumc/projects'", *Query);
    msiExecGenQuery(*Query, *QOut);
    # Gets the continue index value generated by msiExecGenQuery
    # Determine whether there are remaining rows to retrieve from the generated query
    msiGetContInxFromGenQueryOut(*QOut, *cont);
    while (*cont  >= 0){
        foreach (*Row in *QOut) {
            uuChopPath(*Row.COLL_NAME, *collection, *project);
            msi_json_arrayops(*json_str, *project, "add", *size);
        }
        if (*cont == 0){
            break;
        }
        msiGetMoreRows(*Query, *QOut, *cont);
    }

    writeLine("stdout", *json_str);
}

INPUT null
OUTPUT ruleExecOut
